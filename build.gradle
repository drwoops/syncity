plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'java'
}

group = 'me.drwoops'
version = '1.0'

ext {
    minecraft_version = '1.18.2'
    paper_api_version = 'R0.1-SNAPSHOT'
    json_version = '20220320'
    mysql_connector_version = '8.0.29'
}

repositories {
    mavenCentral()
    maven {
        name = 'papermc-repo'
        url = 'https://repo.papermc.io/repository/maven-public/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:${minecraft_version}-${paper_api_version}"
    implementation "org.json:json:${json_version}"
    compileOnly "mysql:mysql-connector-java:${mysql_connector_version}"
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

shadowJar {
    destinationDirectory.set(file("$rootDir/build/plugin"))
    archiveClassifier.set("${minecraft_version}")
    mergeServiceFiles()
}